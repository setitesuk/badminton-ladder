#!/Users/ajb/dev/bin/perl -T
#!/usr/bin/perl -T

use warnings;
use strict;
use lib qw(lib);

BEGIN {
  $ENV{DOCUMENT_ROOT} = './htdocs';
}

use ClearPress::controller;

use badminton_ladder::view::error;
use badminton_ladder::util;

use badminton_ladder::view::player_team;
use badminton_ladder::model::player_team;
use badminton_ladder::view::ladder;
use badminton_ladder::model::ladder;
use badminton_ladder::view::player;
use badminton_ladder::model::player;
use badminton_ladder::view::team;
use badminton_ladder::model::team;
use badminton_ladder::view::ladder_type;
use badminton_ladder::model::ladder_type;

our $VERSION = do { my ($r) = q$LastChangedRevision: 279 $ =~ /(\d+)/smx; $r; };

badminton_ladder::sa->new->run();

0;

package badminton_ladder::sa;
use base qw(HTTP::Server::Simple::CGI);
use strict;
use warnings;
use Data::Dumper;
use Carp;
use English qw(-no_match_vars);

sub handle_request {
  my ($self, $cgi) = @_;

#  print {*STDERR} Dumper($cgi);
#  print {*STDERR} Dumper(%ENV);
  my $EXTN = {
	      css  => 'text/css',
	      xml  => 'text/xml',
	      gif  => 'image/gif',
	      png  => 'image/png',
	      jpg  => 'image/jpeg',
	      txt  => 'text/plain',
	      html => 'text/html',
              js   => 'text/javascript',
	     };

  my $util = badminton_ladder::util->new({
					   cgi => $cgi,
					  });
  print "HTTP/1.0 200 OK\n";

  my ($fn) = "htdocs$ENV{REQUEST_URI}" =~ m|([a-z\d_/\.\-%]+)|mix;
  $fn      =~ s|\.\./||smxg;

  if(-f $fn) {
    my ($ext) = $fn =~ /\.([^\.]+)$/smx;

    my $type  = $EXTN->{lc $ext} || 'application/octet-stream';
    print qq(Content-type: $type\n\n);
    carp qq(Serving static file $fn as $ext / $type);
    open my $fh, $fn or croak "Opening $fn: $ERRNO";
    while(<$fh>) {
      print;
    }
    close $fh or croak "Closing $fn: $ERRNO";
  } else {
    ClearPress::controller->handler($util);
  }

  return 1;
}

sub print_banner {
  my $self = shift;
  print q[ClearPress development server up and running at http://localhost:].$self->port()."/\n";
  return 1;
}

1;
 
